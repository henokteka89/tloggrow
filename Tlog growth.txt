--stored procedure and sql job that will inform tlog growth with its reason. 

USE [DBAUtilization]
GO

/****** Object:  StoredProcedure [dbo].[DBA_Tlog_Usage_Monitoring]    Script Date: 10/3/2023 7:19:07 PM ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO




CREATE PROCEDURE [dbo].[DBA_Tlog_Usage_Monitoring] 
AS
SET NOCOUNT ON

-- step_1: Declare threshold percentage limit and other variables
Declare @emailtreshold INT = 75
Declare @pagethreshold   INT = 85
--Declare @emailTo VARCHAR(200) = 'henokteka89@gmail.com'
--Declare @PageTo VARCHAR(200) = 'kabdahlak123@gmail.com'
Declare @emailTo VARCHAR(200) = 'henokteka87@yahoo.com'
Declare @PageTo VARCHAR(200) = 'henokteka89@gmail.com'
Declare @Emailsubject nvarchar(1000)
Declare @Pagesubject nvarchar(1000)
Declare @tableHTMLEmail nvarchar(max)  = ''
Declare @tableHTMLPage nvarchar(max)  = ''
Declare @SQLDBArunbook nvarchar(max)  
Declare @Emailtitel nvarchar(max)
Declare @env_code  CHAR(3)
IF @@SERVERNAME LIKE 'DE%' --commonservernames
BEGIN
SET @env_code= SUBSTRING(CONVERT(varchar(100),SERVERPROPERTY('machinename')),10,3)
END
ELSE 
BEGIN 
SET @env_code='PD1' --environment
END 
-- step_2: Create temp table and insert sqlperf data into it

IF OBJECT_ID('tempdb..#tlogtables') IS NOT NULL DROP TABLE #tlogtables;
IF OBJECT_ID('tempdb..#tlogtablesFinal') IS NOT NULL DROP TABLE #tlogtablesFinal;

CREATE TABLE #tlogtables
(
   DatabaseName   sysname,
   LogSize        DECIMAL (18, 2),
   LogUsed        DECIMAL (18, 2),
   [status]         INT
)

INSERT INTO #tlogtables
EXECUTE ('DBCC SQLPERF(LOGSPACE)')

--- Get eason why log file usage is increasing and insert into Final table 
Select t.DatabaseName,t.LogSize, cast((t.LogSize*t.LogUsed)/100 as decimal(18,2)) as LogUsed,t.LogUsed as PercentLogUsed,s.log_reuse_wait_desc
Into #tlogtablesFinal
From #tlogtables t
Join  sys.databases s
on s.name =t.DatabaseName
where t.DatabaseName not in ('master','model','msdb','SSISDB','DBAUtilization')

--Select * from #tlogtablesFinal

--- set up alert format
--general title
set @Emailtitel =N'<H3><FONT SIZE="2" FACE="Tahoma">'+@@servername+': High transaction Log File Usage Detected at ' + CONVERT(varchar(30), getdate(), 120) + ' </FONT></H3>'
--Direct to SQLDBA RUNBOOK
set @SQLDBArunbook =N'<H3><FONT SIZE="2" FACE="Tahoma"> '+'For possible actions, please refer SQLDBA RUNBOOK : https:/abc.coom/transaction+Log+File+Usage+Detected' + ' </FONT></H3>'


--set @tableHTML = @tableHTML+ N'<table border="1">' +
set @tableHTMLEmail = @Emailtitel+ N'<table border="1">' +
		N'<FONT SIZE="2" FACE="Calibri">' +            
		N'<tr><th align="center">DatabaseName</th>' +
		N'<th align="center">Log File(s) Size (MB)</th>' +
		N'<th align="center">Log File(s) Used Size (MB)</th>' +
		N'<th align="center">Percent Log Used</th>' +
		N'<th align="center">Log_reuse_wait_desc</th>' +
		N'</tr>' +
				ISNULL(CAST ( ( 
							select td = '',
								td = DatabaseName,'',
								td = LogSize,'',
								td = LogUsed,'',
								td = PercentLogUsed,'',
								td = log_reuse_wait_desc,''
						from #tlogtablesFinal
						 WHERE PercentLogUsed >= @emailtreshold and PercentLogUsed < @pagethreshold 
						FOR XML PATH('tr'), TYPE 
				) AS NVARCHAR(MAX) ),'') +
		N'</FONT>' +
		N'</table>' +
		@SQLDBArunbook;

set @tableHTMLPage = @Emailtitel+ N'<table border="1">' +
		N'<FONT SIZE="2" FACE="Calibri">' +            
		N'<tr><th align="center">DatabaseName</th>' +
		N'<th align="center">Log File(s) Size (MB)</th>' +
		N'<th align="center">Log File(s) Used Size (MB)</th>' +
		N'<th align="center">Percent Log Used</th>' +
		N'<th align="center">Log_reuse_wait_desc</th>' +
		N'</tr>' +
				ISNULL(CAST ( ( 
							select td = '',
								td = DatabaseName,'',
								td = LogSize,'',
								td = LogUsed,'',
								td = PercentLogUsed,'',
								td = log_reuse_wait_desc,''
						from #tlogtablesFinal
						 WHERE PercentLogUsed >= @pagethreshold
						FOR XML PATH('tr'), TYPE 
				) AS NVARCHAR(MAX) ),'') +
		N'</FONT>' +
		N'</table>' +
		@SQLDBArunbook;

-- step_4: send email or page if a T-log usage exceeds threshold
set @Emailsubject =  'SQL Alert (Warning) '+@env_code+' - SQL Server TLog file usage is greater than 75 %  on' + ' '  + @@servername
set @Pagesubject =   'SQL Alert (Critical) '+@env_code+' - SQL Server TLog file usage is greater than or equal to 85 %  on' + ' '  + @@servername
IF EXISTS (Select 1 FROM #tlogtablesFinal WHERE PercentLogUsed >= @emailtreshold and PercentLogUsed < @pagethreshold) 
   BEGIN
      EXEC msdb.dbo.Sp_send_dbmail @profile_name = 'HenokEmailProfile',
                                   @body = @tableHTMLEmail,
								   @body_format = 'HTML',
                                   @recipients   = @emailTo,
                                   @subject      = @Emailsubject ;

				 EXEC xp_logevent 50001, @Emailsubject, 'Warning';  
   END

IF EXISTS (Select 1 FROM #tlogtablesFinal WHERE PercentLogUsed >= @pagethreshold)  
BEGIN
    EXEC msdb.dbo.Sp_send_dbmail @profile_name = 'HenokEmailProfile',
                                @body = @tableHTMLPage,
								@body_format = 'HTML',
                                @recipients   = @PageTo,
                                @subject      = @Pagesubject ;


			EXEC xp_logevent 50001, @Pagesubject, 'ERROR';  
END
GO
---
USE [msdb]
GO

/****** Object:  Job [DBA Tlog File Usage Monitoring]    Script Date: 10/3/2023 8:27:03 PM ******/
BEGIN TRANSACTION
DECLARE @ReturnCode INT
SELECT @ReturnCode = 0
/****** Object:  JobCategory [[Uncategorized (Local)]]    Script Date: 10/3/2023 8:27:03 PM ******/
IF NOT EXISTS (SELECT name FROM msdb.dbo.syscategories WHERE name=N'[Uncategorized (Local)]' AND category_class=1)
BEGIN
EXEC @ReturnCode = msdb.dbo.sp_add_category @class=N'JOB', @type=N'LOCAL', @name=N'[Uncategorized (Local)]'
IF (@@ERROR <> 0 OR @ReturnCode <> 0) GOTO QuitWithRollback

END

DECLARE @jobId BINARY(16)
EXEC @ReturnCode =  msdb.dbo.sp_add_job @job_name=N'DBA Tlog File Usage Monitoring', 
		@enabled=1, 
		@notify_level_eventlog=0, 
		@notify_level_email=2, 
		@notify_level_netsend=0, 
		@notify_level_page=0, 
		@delete_level=0, 
		@description=N'No description available.', 
		@category_name=N'[Uncategorized (Local)]', 
		@owner_login_name=N'sa', 
		@notify_email_operator_name=N'DBAHenok', @job_id = @jobId OUTPUT
IF (@@ERROR <> 0 OR @ReturnCode <> 0) GOTO QuitWithRollback
/****** Object:  Step [Check Primary]    Script Date: 10/3/2023 8:27:03 PM ******/
EXEC @ReturnCode = msdb.dbo.sp_add_jobstep @job_id=@jobId, @step_name=N'Check Primary', 
		@step_id=1, 
		@cmdexec_success_code=0, 
		@on_success_action=3, 
		@on_success_step_id=0, 
		@on_fail_action=1, 
		@on_fail_step_id=0, 
		@retry_attempts=0, 
		@retry_interval=0, 
		@os_run_priority=0, @subsystem=N'TSQL', 
		@command=N'DECLARE @current_role TINYINT;
				SET @current_role = (
				   SELECT ROLE 
				   FROM sys.dm_hadr_availability_replica_states 
				   WHERE is_local = 1
				);
				IF @current_role <> 1
					RAISERROR(''This node is not currently primary'',16,1)
				', 
		@database_name=N'master', 
		@flags=0
IF (@@ERROR <> 0 OR @ReturnCode <> 0) GOTO QuitWithRollback
/****** Object:  Step [Tlog file usage Alerts]    Script Date: 10/3/2023 8:27:03 PM ******/
EXEC @ReturnCode = msdb.dbo.sp_add_jobstep @job_id=@jobId, @step_name=N'Tlog file usage Alerts', 
		@step_id=2, 
		@cmdexec_success_code=0, 
		@on_success_action=1, 
		@on_success_step_id=0, 
		@on_fail_action=2, 
		@on_fail_step_id=0, 
		@retry_attempts=0, 
		@retry_interval=0, 
		@os_run_priority=0, @subsystem=N'TSQL', 
		@command=N'EXEC [DBAUtilization].[dbo].[DBA_Tlog_Usage_Monitoring]', 
		@database_name=N'DBAUtilization', 
		@flags=0
IF (@@ERROR <> 0 OR @ReturnCode <> 0) GOTO QuitWithRollback
EXEC @ReturnCode = msdb.dbo.sp_update_job @job_id = @jobId, @start_step_id = 1
IF (@@ERROR <> 0 OR @ReturnCode <> 0) GOTO QuitWithRollback
EXEC @ReturnCode = msdb.dbo.sp_add_jobschedule @job_id=@jobId, @name=N'TLog File Usage Monitoing Schedule', 
		@enabled=1, 
		@freq_type=4, 
		@freq_interval=1, 
		@freq_subday_type=8, 
		@freq_subday_interval=1, 
		@freq_relative_interval=0, 
		@freq_recurrence_factor=0, 
		@active_start_date=20201128, 
		@active_end_date=99991231, 
		@active_start_time=0, 
		@active_end_time=235959, 
		@schedule_uid=N'6febd1af-519d-4e93-a4b1-4792b7258017'
IF (@@ERROR <> 0 OR @ReturnCode <> 0) GOTO QuitWithRollback
EXEC @ReturnCode = msdb.dbo.sp_add_jobserver @job_id = @jobId, @server_name = N'(local)'
IF (@@ERROR <> 0 OR @ReturnCode <> 0) GOTO QuitWithRollback
COMMIT TRANSACTION
GOTO EndSave
QuitWithRollback:
    IF (@@TRANCOUNT > 0) ROLLBACK TRANSACTION
EndSave:
GO




